# Lazy-load completions for better shell startup performance
# Completions are loaded on first use of each command
# This file is zsh-specific - skip if not running zsh

[[ -z "$ZSH_VERSION" ]] && return

# Docker completion
if type docker > /dev/null 2>&1; then
  docker() {
    unfunction docker
    source <(command docker completion zsh) 2>/dev/null
    command docker "$@"
  }
fi

# Stern completion
if type stern > /dev/null 2>&1; then
  stern() {
    unfunction stern
    source <(command stern --completion=zsh) 2>/dev/null
    command stern "$@"
  }
fi

# Kubectl completion
if type kubectl > /dev/null 2>&1; then
  kubectl() {
    unfunction kubectl
    source <(command kubectl completion zsh) 2>/dev/null
    command kubectl "$@"
  }
fi

# Kops completion
if type kops > /dev/null 2>&1; then
  kops() {
    unfunction kops
    source <(command kops completion zsh) 2>/dev/null
    command kops "$@"
  }
fi

# Kind completion
if type kind > /dev/null 2>&1; then
  kind() {
    unfunction kind
    source <(command kind completion zsh) 2>/dev/null
    command kind "$@"
  }
fi

# Chezmoi completion
if type chezmoi > /dev/null 2>&1; then
  chezmoi() {
    unfunction chezmoi
    source <(command chezmoi completion zsh) 2>/dev/null
    command chezmoi "$@"
  }
fi

# Ngrok completion
if type ngrok > /dev/null 2>&1; then
  ngrok() {
    unfunction ngrok
    source <(command ngrok completion) 2>/dev/null
    command ngrok "$@"
  }
fi

# OrbStack completion (always load as it's lightweight)
if type orbctl > /dev/null 2>&1; then
  source ~/.orbstack/shell/init.zsh 2>/dev/null || :
fi

__aws_sso_profile_complete() {
  local _args=${AWS_SSO_HELPER_ARGS:- -L error}
  _multi_parts : "($(/opt/homebrew/bin/aws-sso ${=_args} list --csv Profile))"
}

aws-sso-profile() {
  local _args=${AWS_SSO_HELPER_ARGS:- -L error}
  if [ -n "$AWS_PROFILE" ]; then
    echo "Unable to assume a role while AWS_PROFILE is set"
    return 1
  fi

  if [ -z "$1" ]; then
    echo "Usage: aws-sso-profile <profile>"
    return 1
  fi

  eval $(/opt/homebrew/bin/aws-sso ${=_args} eval -p "$1")
  if [ "$AWS_SSO_PROFILE" != "$1" ]; then
    return 1
  fi
}

aws-sso-clear() {
  local _args=${AWS_SSO_HELPER_ARGS:- -L error}
  if [ -z "$AWS_SSO_PROFILE" ]; then
    echo "AWS_SSO_PROFILE is not set"
    return 1
  fi
  eval $(/opt/homebrew/bin/aws-sso ${=_args} eval -c)
}

compdef __aws_sso_profile_complete aws-sso-profile 2>/dev/null

# but-pull-all completion - complete GitButler project names and flags
__but_pull_all_complete() {
  local projects_file="$HOME/Library/Application Support/com.gitbutler.app/projects.json"
  _arguments \
    '(-f --force)'{-f,--force}'[Clean broken refs automatically]' \
    '(-t --timeout)'{-t,--timeout}'[Timeout in seconds per repo (default: 30)]:timeout:' \
    '*:project:->projects'

  if [[ $state == projects ]] && [[ -f "$projects_file" ]]; then
    local projects=($(jq -r '.[].path' "$projects_file" 2>/dev/null | xargs -I{} basename {} | sort -u))
    _describe 'project' projects
  fi
}

compdef __but_pull_all_complete but-pull-all 2>/dev/null

# k8s-rds-tunnel completion - complete cluster names and flags
__k8s_rds_tunnel_complete() {
  _arguments \
    '(-p --port)'{-p,--port}'[Local port (default: 35432)]:port:' \
    '*:cluster:->clusters'

  if [[ $state == clusters ]] && [[ -n "$K8S_RDS_CONFIG" ]] && [[ -f "$K8S_RDS_CONFIG" ]]; then
    local clusters=()
    while IFS= read -r line; do
      [[ "$line" =~ ^[[:space:]]*# ]] && continue
      [[ -z "${line// }" ]] && continue
      clusters+=("${line%% *}")
    done < "$K8S_RDS_CONFIG"
    _describe 'cluster' clusters
  fi
}

compdef __k8s_rds_tunnel_complete k8s-rds-tunnel 2>/dev/null
